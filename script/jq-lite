#!/usr/bin/env perl

use strict;
use warnings;
use JSON::PP;
use FindBin;
use lib "$FindBin::Bin/../lib";
use JQ::Lite;

# Argument parsing
my $raw_output = 0;
my $query;

for my $arg (@ARGV) {
    if ($arg eq '--raw-output' || $arg eq '-r') {
        $raw_output = 1;
    } elsif ($arg eq '--help' || $arg eq '-h') {
        print <<'USAGE';
Usage: jq-lite [--raw-output|-r] [--help|-h] '.query'

Examples:
  cat users.json | jq-lite '.users[0].name'
  cat users.json | jq-lite -r '.users[].name'
  cat users.json | jq-lite '.users[] | select(.age > 25 and .profile.country == "JP")'
USAGE
        exit 0;
    } elsif (!defined $query && $arg =~ /^[^.]/) {
        # Unrecognized option (e.g. --h)
        die "Error: Unknown option '$arg'\nUsage: jq-lite [--raw-output|-r] [--help|-h] '.query'\n";
    } elsif (!defined $query) {
        $query = $arg;
    } else {
        die "Usage: jq-lite [--raw-output|-r] '.query'\n";
    }
}

die "Usage: jq-lite [--raw-output|-r] '.query'\n" unless defined $query;

# Read JSON text from STDIN
my $json_text = do { local $/; <STDIN> };

# Run query
my $jq = JQ::Lite->new(raw => $raw_output);
my @results = $jq->run_query($json_text, $query);

# JSON formatter for output
my $pp = JSON::PP->new->utf8->canonical->pretty;

# Print results
for my $r (@results) {
    if (!defined $r) {
        print "null\n";
    } elsif ($raw_output && ref($r) eq '') {
        print "$r\n";  # Raw string output
    } else {
        print $pp->encode($r);  # Formatted JSON output
    }
}

